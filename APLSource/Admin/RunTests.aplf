 RunTests←{
     ⍝ ⍺ ←→ StopOnFail 0|1, StopOnBroken 0|1, Quiet 0|1|2
     ⍝ ⍵ ←→ Test Namespace
     ⍝ ← ←→ Result space
     ⎕IO ⎕ML←0 1
     ⍺←0
     (tf tb q)←(~2↑⍺),⊃⌽3↑⍺
     s←'Passed' 'Failed' 'Broken' 'N/A' 'Disabled'
     d←↓↑':',¨⍨s,¨⍨' ',¨⍨3/¨1 4/' !'
     Display←{
         ⍺>1:0
         r←z.Result
         h←'Number of tests:'(≢r)
         ⎕←'*'⍪(⍕h⍪s,⍪+⌿r∘.=⍳5)⍪'*'
         ⎕←'Code coverage: ',,'Q<%>I4'⎕FMT ⍵.Coverage
         ⍵.Coverage=100:0
         ⎕←'Untested code:'
         ⎕←↑⍵.Untested
         0
     }
     Execute←{
         tb/1+⍳99::2
         tf/789::1
         ⍺⍎⍵,' 0'
     }
     Run←{
         b←⍺ Execute ⍵
         q>0:b
         ⍝b⊣⎕←⍵,': ',b⊃d
         b⊣⎕←(b⊃d),' ',⍵
     }
     Assert←{
         0⊣'TEST FAILED'⎕SIGNAL 789/⍨~⍵
     }
     Try←{
         ⍺←⊢
         0::⎕DMX.EN
         9999⊣⍺ ⍺⍺ ⍵
     }
     _←⍵.⎕FX¨⎕NR¨'Assert' 'Try'
     z←⎕NS''
     z.Function←'T'⍵.⎕NL ¯3
     _←⎕PROFILE'start' 'coverage'
     z.Result←⍵ Run¨z.Function
     _←⎕PROFILE'stop'
     z.(Coverage Untested)←Coverage 0
     1:z←z⊣q Display z
 }
