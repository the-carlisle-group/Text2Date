 RunTests←{
     ⍝ ⍺ ←→ StopOnFail 0|1, StopOnBroken 0|1, Quiet 0|1|2
     ⍝ ⍵ ←→ Test space, Code Space
     ⍝ ← ←→ Result space
     ⎕IO ⎕ML←0 1
     ⍺←0
     (tf tb q)←(~2↑⍺),⊃⌽3↑⍺
     (ts cs)←⍵
     s←'Passed' 'Failed' 'Broken' 'N/A' 'Disabled'
     d←↓(↑':',¨⍨s),' ',↑3/¨' !!--'
     Spaces←{
         ⍵≢⍕⍎⍵:⍬
         s←⍵∘,¨'.',¨(⍎⍵).⎕NL ¯9
         0=≢s:⍎⍵
         ∊(⍎⍵),∇¨s
     }
     Coverage←{
         m←⍵
         n←m.⎕NL-3 4
         af←(⍕m)∘,¨'.',¨n
         d←⎕PROFILE'data'
         b←d[;1]∊⊂⍬
         ef←b/d[;0]
         el←(+\b)⊆d[;1]
         nr←(⎕NR¨af)~¨⊂⊂' }'
         al←⍳¨≢¨nr
         ul←al~¨(el,⊂⍬)[ef⍳af]
         c←100×1-÷/≢¨∊¨ul al
         k←0<≢¨ul
         uf←~af∊ef
         (uf/ul)←⊂⍬
         z←k/af{0=≢⍵:⍺ ⋄ ⍺,'[',(⍕⍵),']'}¨ul  ⍝
         c z
     }
     Display←{
         ⍺>1:0
         r←z.Result
         h←'Number of tests:'(≢r)
         ⎕←'*'⍪(⍕h⍪s,⍪+⌿r∘.=⍳5)⍪'*'
         ⎕←'Code coverage: ',,'Q<%>I4'⎕FMT ⍵.Coverage
         ⍵.Coverage=100:0
         ⎕←'Untested code:'
         ⎕←↑⍵.Untested
         0
     }
     Execute←{
         tb/1+⍳99::2
         tf/789::1
         ⍺⍎⍵,' 0'
     }
     Run←{
         b←⍺ Execute ⍵
         q>0:b
         b⊣⎕←(b⊃d),' ',⍵
     }
     Assert←{
         0⊣'TEST FAILED'⎕SIGNAL 789/⍨~⍵
     }
     Try←{
         ⍺←⊢
         0::⎕DMX.EN
         9999⊣⍺ ⍺⍺ ⍵
     }
     _←⍵.⎕FX¨⎕NR¨'Assert' 'Try'
     z←⎕NS''
     z.Function←'T'ts.⎕NL ¯3
     _←⎕PROFILE'start' 'coverage'
     z.Result←⍵ Run¨z.Function
     _←⎕PROFILE'clear'
     z.(Coverage Untested)←Coverage cs
     1:z←z⊣q Display z
 }
