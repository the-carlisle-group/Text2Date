 ErrorCheck←{
     0=⎕NC'⍺':2 Signal'The function requires a left argument'
     m←↑⍵
     c←2=≢⍴m      ⍝ Is char data
     n←1=≢⍴m      ⍝ Is Numeric datea
     ⍝ 80≠⎕DR m:11 Signal'The right argument must be a simple char mat or simple numeric vector'
     f w t←ProcessLeftArgument ⍺
     r←CheckPattern¨f
     e←~r∊⊂''
     ∨/e:11 Signal⊃e/r
     u←(∧/¨f∊¨⊂'YMDhms')∨(⎕C f)∊'excel'∘,¨'' '1900' '1904'
     n∧~∧/u:11 Signal'Numeric data requires undelimited patterns.'
     f←{1=≢⍵:⊃⍵ ⋄ ⍵}f
     f w t m
 }
