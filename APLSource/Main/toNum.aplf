 toNum←{                     ⍝ 2d char to 1d num
     0=↑⍴⍵:⍳0                ⍝ empty
     s←⎕UCS 13               ⍝ delim
     c←,s,⍵                  ⍝ on every line
     c[0]←' '                ⍝ lose first delim
     c/⍨←~'  '⍷c             ⍝ squeeze
     c≡,' ':,0
     v r←s ⎕VFI c            ⍝ try it
     ∧/v:r                   ⍝ all ok then ok
     c/⍨←0                   ⍝ wipe out
     v=←0                    ⍝ in situ ~
     d←s,'0123456789.-¯(,+E' ⍝ valid
     v∧←v⍀∨/(v⌿⍵)∊d          ⍝ ignore if none
     ~∨/v:r                  ⍝ none!
     c←,s,v⌿⍵                ⍝ these need some work
     c[0]←' '                ⍝ lose first delim again
     c[{⍵/⍳⍴⍵}~c∊d]←' '      ⍝ lose invalid chars
     c/⍨←~'  '⍷c             ⍝ squeeze
     c[{⍵/⍳⍴⍵}c∊'-(']←'¯'    ⍝ negs
     c/⍨←~c∊',+'             ⍝ remove commas and +
     r[v/⍳⍴v]←1⊃s ⎕VFI c     ⍝ and try again
     r                       ⍝ 1 num per ⍵ row
 }
