 ParseVariable←{
     ⍝ ⍺ ←→ Variable width format
     ⍝ ⍵ ←→ Character matrix of time strings
     ⍝ ← ←→ Time matrix
     m←' ',⍺ VariableMMM ⍵     ⍝ Convert Jan, et.c Pad
     f←∪⍺∩'YMDhms'             ⍝ Strip any delims and redundant elements
     b←m∊⎕D                    ⍝ digit runs
     r←+/2</b                  ⍝ Count the digit runs
     k←≢f                      ⍝ Number of desired elements
     n←⊃r                      ⍝ Number of actual elements
     r∨.≠n:⍺ ∇ r Pad ⍵         ⍝ Not all same, try again
     v←,m                      ⍝ Ravel
     v[⍸~,b]←' '               ⍝ Blank out all delims
     ⍉(⌽n,≢m)⍴1⊃⎕VFI v         ⍝ Convert to numeric matrix
 }
