 ProcessFixed←{
     ⍝ ⍵ ←→ Character matrix of time strings
     ⍝ ⍺ ←→ Fixed width format, Century Window
     ⍝ ← ←→ Decimal Encoded Type 60 time number
     f w←⍺
     c←1∊'YYYY'⍷f      ⍝ Have century
     y←⊂'Y'⍴⍨2*1+c     ⍝ Year element
     e←y,2⍴¨'MDhms'    ⍝ The elements
     b←e⍷¨⊂f           ⍝ Search for elements
     e b←(1∊¨b)∘/¨e b  ⍝ Elements provided
     i←∊(⍸¨b)+⍳¨≢¨e    ⍝ Indices
     m←⍵[;i]           ⍝ Re-order
     v←1⊃⎕VFI,' ',m    ⍝ To numeric
     s←{⍵-6-⍵}≢e       ⍝ Scale factor
     d←100 1+.×1 2≥≢e  ⍝ Default months/days factor
     d+v×s
 }
