 z←toDateCover x                         ⍝ Public method
 :Access Public                          ⍝ Numerics handled by ##.NumericFunctions.toDate
 z←⎕THIS{                                ⍝ ⍺←→Instance space
     df dw←{0=≢⍵:(⊃'ymd' 'mdy' 'dmy')50  ⍝ No formats
         l←1⊃##.Check.vf_Objects ⍵
         2↑50,⍨⊆l.Value
     }⍵
     df←##.Core.(ntbs low)df
     la←df{⍺ ⍵}¨dw
     g←{
         f←##.Core.toDate
         z←0⍴⍨≢⍵
         _←la{
             0=≢⍺:0
             i←⍸z=0
             0=≢i:0
             z[i]←(↑⍺)f ⍵[i;]
             (1↓⍺)∇ ⍵
         }⍵
         z
     }
     v←⍺._Value
     new←##.Date.New
     d←⍺.Structure
     d=0:new(↑la g,[¯0.5]v)0 d
     d=1:new(la g v)0 d
     d=2:new(⊂la g⊃v)0 d
     d=3:new(la∘g¨v)0 d
 }x
