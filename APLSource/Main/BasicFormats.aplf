 BasicFormats←{
     ⍝ ⍵ ←→ Time Strings
     ⍝ ← ←→ Potential appropriate format strngs
     m←↑⍵
     w←1⊃⍴m
     toWide←w∘{⍵/⍨⍺≥≢¨⍵}
     noDays←{⍵/⍨~'D'∊¨⍵}
     hasDays←{⍵/⍨'D'∊¨⍵}
     i←⊂¨∪⍋¨,⍳3⍴3
     sn←1∊∊(↓MonthNames'')⍷¨⊂⎕C m
     fp←,i∘.⌷((4⍴sn 1)/(4 3 2)(4 2 2)(2 3 2)2)/¨¨⊂'YMD'
     fp←fp,∪fp~¨⊂⊂'DD'
     fd←1↓¨∊¨'/',¨¨fp
     fu←{⍵/⍨~∨/'MMM'⍷↑⍵}∊¨fp
     v←i∘.⌷⊂'YMD'
     v,←sn/(1+2×v='M')/¨v
     v,←∪v~¨'D'
     b←⎕D∊⍨' ',m
     r←+/2</b                              ⍝ Digit runs
     d←+/~b[0;]                            ⍝ Number of delims
     fw←1=≢∪b                              ⍝ Fixed width
     1∧.≥r:fu,⊂'excel' ⍝ toWide fu         ⍝ No delimiters
     fw∧(d=1)∧2∧.=r:noDays toWide fd       ⍝ Fixed without days
     fw∧(d=2)∧3∧.=r:hasDays toWide fd      ⍝ Fixed with days
     z←toWide fu,fd,v
     z←z[⍒≢¨z]                             ⍝ Preference for longer format.
     z[⍒+/¨'YMD'∘∊¨z]                      ⍝ Preference for more elements
 }
