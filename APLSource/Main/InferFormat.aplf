 InferFormat←{
     ⍝ ⍺ ←→ First Day of Month Bias
     ⍝ ⍵ ←→ Matrix of time strings
     ⍝ ← ←→ Info
     ⍺←0
     a←BasicFormats ⍵
     e←300+⍳100
     ok←a{e::0 ⋄ 1⊣⍺ ErrorCheck ⍵}¨⊂⍵
     f←ok/a
     r←f{⍺ Text2Date ⍵}¨⊂⍵
     r←r×19000000 22000000∘(0=⍸)¨r
     b←0≠r
     n←+/¨b
     af←1<⊃+/b
     d←(∨/¨b)∧∧/¨1=100⊤¨b/¨r
     j←⍺∧(n=⌈/n)∧(~d)∧∨/d
     n b f←(~j)∘/¨n b f
     g←⍒n
     k←(+/¨<\b[g])[⍋g]
     m←(↑f n k)[;g]
     s←⎕NS''
     s.(AllFormats Absolute Relative)←(m[1;]>0)∘⌿¨↓m
     s.Ambiguous←(=/2↑s.Absolute)∧=/≢¨2↑s.AllFormats
     s.Formats←s.AllFormats/⍨s.Relative>0
     s.PrimaryFormat←⊃s.Formats
     s.AmbiguousFlag←af
     s
 }
